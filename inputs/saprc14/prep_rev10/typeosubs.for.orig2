C	TYPEOSUBS.FOR
C
C
C       Subroutines included to support the TYPEO.for procedures
C
C		TSPCNAM - map typed specied to array index, add if new
C		TPNTRXN - printout the typed reactions to name.pro2 file
c		TYPRXN  - type the reaction
C
C
C
C -----------------------------------------------------------------------------
c
c      Check to see if reaction irxn contains species tname(k), hence typing
C
       subroutine TYPRXN(k,irxn)
       include 'pspecs.inc'
       character aname*16, sname*21, suffix*30, sufgen*30
       integer i,j,k,irxn
       real    tno(0:25)
       logical newrxn
c
c copy the traced atom name to the working variable
c
       write(aname,'a16') tatom(k)
       call movlft(aname)
c
c check if this reaction has already been typed for the target atom
c
      if(index(srxnlbl(irxn),aname(:1)).ne.0)return
c
c calculate the number of traced atoms in the reactant list, and count
c the species with traced atoms
c
      tr = 0.0
      nr = 0
      c1 = 1.0
      icount = 0
      do i = 1,nrtosr(irxn)
       icount = icount + 1
       if(irtosr(i,irxn).gt.0)then
c contribution from this reactant
        if(aname(:1).eq.'N')then
         tr = tr + c1*nno(irtosr(i,irxn))
         tno(icount) = nno(irtosr(i,irxn))
        elseif(aname(:1).eq.'S')then
         tr = tr + c1*sno(irtosr(i,irxn))
         tno(icount) = sno(irtosr(i,irxn))
        elseif(aname(:1).eq.'C')then
         tr = tr + c1*cno(irtosr(i,irxn))
         tno(icount) = cno(irtosr(i,irxn))
        elseif(aname(:1).eq.'X')then
         tr = tr + c1*xno(irtosr(i,irxn))
         tno(icount) = xno(irtosr(i,irxn))
        endif
c count the number of reactants with traced atoms
        if(tno(icount).gt.0)nr = nr + 1
c fractional coefficients indicate that this is not an elementary rxn
c this rxn will need to be written as elementary steps for typing 
        if(amod(c1,1.0).ne.0.0 .and. tno(icount).gt.0)then
         print*,'non-integer coefficient for rxnlbl:',rxnlbl(irxn)
         print*,'coef,species:',c1,name(irtosr(i,irxn))
         print*,'rewrite rxn as elementrary steps for typing'
         print*,'only CONSTANT int coefs are allowed for typed reac'
         stop 'marker1'
        endif
        c1 = 1.0
       else
c possible leading coefficient
        if(-irtosr(i,irxn).lt.maxco)c1 = c1*coef(-irtosr(i,irxn))
        tno(icount) = 0
       endif
      enddo
      print*,'# traced reactant atoms:',tr
c
c calculate the number of traced atoms in the product list
c
      tp = 0.0
      c1 = 1.0
      do i = nprods(irxn),nprods(irxn+1)-1
       icount = icount + 1
       if(iprods(i).gt.0)then
c contribution from this product
        if(aname(:1).eq.'N')then
         tp = tp + c1*nno(iprods(i))
         tno(icount) = nno(iprods(i))
        elseif(aname(:1).eq.'S')then
         tp = tp + c1*sno(iprods(i))
         tno(icount) = sno(iprods(i))
        elseif(aname(:1).eq.'C')then
         tp = tp + c1*cno(iprods(i))
         tno(icount) = cno(iprods(i))
        elseif(aname(:1).eq.'X')then
         tp = tp + c1*xno(iprods(i))
         tno(icount) = xno(iprods(i))
        endif
c fractional coefficients indicate that this is not an elementary rxn
c this rxn will need to be written as elementary steps for typing unless
c we only have a single typed reactant 
        if(amod(c1,1.0).ne.0.0 .and. tno(icount).gt.0 .and. nr.gt.1)then
         print*,'non-integer coefficient for rxnlbl:',rxnlbl(irxn),nr
         print*,'coef,name:',c1,name(iprods(i))
         print*,'rewrite rxn as elementrary steps for typing'
         stop 'marker2'
        endif
        c1 = 1.0
       else
c possible leading coefficient
        if(-iprods(i).le.maxco .and. -iprods(i).gt.maxcov)then
         c1 = c1*coef(-iprods(i))
        else
         c1 = 0
        endif
        tno(icount) = 0
       endif
      enddo
      print*,'# traced product atoms:',tp
c
c make sure the reactant and product sums are equal
c
      if(tr.le.0.0 .or. amod(tr,1.0).ne.0.0 .or. tp.gt.tr)then
       print*,'mismatch for reaction tracing:',rxnlbl(irxn),aname(:1)
       print*,'this may be caused by variable coefs used as '//
     +        'product stoichiometric values'
       print*,'only constant integer values can be stoichiometric '//
     +        'values'
       print*,'tr, tp:',tr,tp
       print*,'reactant, tno:'
       icount = 0
       do i = 1,nrtosr(irxn)
        icount = icount + 1
        j = irtosr(i,irxn)
        if(j.gt.0)then
         print*,name(j),tno(icount)
        elseif(-j.le.maxcov)then
         print*,'constant to modify K',coefnm(-j)
        elseif(-j.le.maxco)then
         print*,'constant to modify K',coef(-j)
        endif
       enddo
       print*,'product, tno:'
       do i = nprods(irxn),nprods(irxn+1)-1
        icount = icount + 1
        j = iprods(i)
        if(j.lt.0 .and.-j.le.maxcov)then
         print*,'coeff preceding next spec:',coefnm(-j),coef(-j)
        elseif(j.lt.0 .and. -j.le.maxco)then
         print*,'coeff preceding next spec:',coef(-j)
        else
         print*,name(j),tno(icount)
        endif
       enddo
       stop 'marker1'
      endif
c write a waring if this reaction will discard traced information
      if(int(tp).le.0)then
       print*,'warning - traced information discarded for rxn:',
     +         rxnlbl(irxn), aname(:1)
      endif
c
c check the typed reactants to make sure they exist; add them if not.
c note - we add the typed species to the regular list, and the 
c base species to the traced list
c
      icount = 0
      do i = 1,nrtosr(irxn)
       icount = icount + 1
       if(irtosr(i,irxn).gt.0)then
        call chktspc2(aname,tno(icount),irtosr(i,irxn),k)
        if(tno(icount).gt.0)then
         call tspcnam(isp,name(irtosr(i,irxn)),.true.)
         tatom(isp) = tatom(k)
         ttypes(isp) = ttypes(k)
        endif
       endif
      enddo
c
c check the typed products to make sure they exist; add them if not.
c note - we add the typed species to the regular list, and the 
c base species to the traced list
c
      do i = nprods(irxn),nprods(irxn+1)-1
       icount = icount + 1
       if(iprods(i).gt.0)then
        call chktspc2(aname,tno(icount),iprods(i),k)
        if(tno(icount).gt.0)then
         call tspcnam(isp,name(iprods(i)),.true.)
         tatom(isp) = tatom(k)
         ttypes(isp) = ttypes(k)
        endif
       endif
      enddo
c
c go through and type this reaction. note that we skip the first
c index because this is the vanilla reaction.  nt1 is the total
c number of sources to type.  nt is the total number of reactions
c generated.
c
      nt1 = ttypes(k) + 1
      nt = nt1**int(tr)
c      print*,'typing reaction :',irxn,tr,nt
      do 10 i = 2,nt ! top of the added reaction loop
c
c index the reaction count and check to see if it exceeds maxrxn
c
       nrxn = nrxn + 1
       if(nrxn.gt.maxrxn)then
        write(out,*)'TOO MAN REACTIONS.  MAX=',maxrxn
        stop 'too many reactions in typrxn. increase maxrxn'
       endif
c
c set the new reaction label as a counter followed by the atom symbol
c note that the primary rxnlbl has the added reaction count followed by
c the traced atom.  the secondary srxnlbl only contains the traced atom
c so that we can quickly determine if the reaction has been typed.
c
c primary rxn label for new rxn
       tnrxnlbl = tnrxnlbl + 1
       write(rxnlbl(nrxn),'(i5,a1)')tnrxnlbl,aname(:1)
       call movlft2(rxnlbl(nrxn))
c       print*,'new rxn lbl:**'//rxnlbl(nrxn)//'**'
c secondary rxn label for new rxn
       i1 = max(nblank(srxnlbl(nrxn)),1)
       if(i1.gt.0)then
        write(srxnlbl(nrxn),'(a)')srxnlbl(nrxn)(:i1)//aname(:1)
       else
        write(srxnlbl(nrxn),'(a)')aname(:1)
       endif
       call movlft2(srxnlbl(nrxn))
c       print*,'new srxn lbl:**'//srxnlbl(nrxn)//'**'
c
c set the new reaction type and check to see if irxn is a samek
c
       rxtyp(nrxn) = 0
       if(rxtyp(irxn).eq.0)then
        lkbuf(nrxn) = lkbuf(irxn)
       else
        lkbuf(nrxn) = irxn
       endif
c
c set the number of reactants
c
       nrtosr(nrxn) = nrtosr(irxn)
c
c set the number of products.  this is tricky because we allow integer
c coefficients in the product list.
c
c note: in the first revision of typed soam we considered a special
c case where n2o5_N1_N2 -> 0.5 no2_N1 + 0.5 no2_N2 + 0.5 no3_N1 + 0.5 no3_N2
c but now we simplify to n2o5_N1_N2 -> no2_N1 + no3_N2 because this seems
c more generally correct for soa source apportionment (and a lot easier to
c program)
c
       icount = nrtosr(irxn)
       c1 = 1.0
       ip = 0
       print*,'# base products:',nprods(irxn+1)-nprods(irxn)
       do j = nprods(irxn),nprods(irxn+1)-1
        icount = icount + 1
        print*,'j,iprods(j):',j,iprods(j)
        if(iprods(j).gt.0 .and. tno(icount).gt.0)then
c count any leading coefficients and calculate their product
         c1 = 1.0
         do j2 = j-1,nprods(irxn),-1
          if(iprods(j2).gt.0)goto 50
c MJK changed this 1/31/05
          if(-iprods(j2).le.maxco .and. -iprods(i).gt.maxcov)then
           c1 = c1 * coef(-iprods(j2))
          endif
c MJK changed this 1/31/05
         enddo
 50      continue
         nlc = j - 1 - j2 
c if we have an integer leading coefficient, then expand the products.  otherwise
c check to make sure we have a single typed reactant, and continue
         print*,'c1,nlc:',c1,nlc
         if(amod(c1,1.0).eq.0.0)then
          ip = ip - nlc + int(c1)
         elseif(nr.le.1)then
          ip = ip + 1
         else
          print*,'somehow a fractional product coeficient exists for ',
     +       'a reaction with more than 1 typed reactant',rxnlbl(irxn)
          stop 'marker1'
         endif
        elseif(iprods(j).lt.0 .and. -iprods(j).lt.maxco)then
         c1 = c1 * coef(-iprods(j))
         ip = ip + 1
        else
         ip = ip + 1
        endif
       enddo
       nprods(nrxn+1) = nprods(nrxn) + ip 
       if(nprods(nrxn+1).gt.maxprd)then
        write(icrt,'too many products in expansions')
        stop 'too many products in typrxn'
       endif
       print*,'number of typed products:',ip
c
c generate the suffix for this index location.  note: this is the 
c combined suffix for all reactants or all products.  we will need
c to take pieces of this suffix for each species.
c
       suffix = sufgen(i,int(tr),nt1,aname(:1))
c       print*,'rxn suffix**'//suffix(:nblank(suffix))//'**'
c       print*,'rxn suffix**'//suffix//'**'
       icount = 0
       outbuf = ' '
c
c set the reactant numbers
c
       i1 = 1
       do j = 1,nrtosr(nrxn)
        icount = icount + 1
        if(tno(icount).gt.0)then
c find the proper substring of the suffix; i1 is the start of the current
c substring, and i2 is the start of the next substring
         isum = 0
         do i2 = i1,len(suffix)
          if(suffix(i2:i2+1).eq.'_'//aname(:1) .or. 
     +       suffix(i2:i2).eq.' ')isum=isum+1
          if(isum.gt.tno(icount))goto 20
         enddo
 20      continue 
c now check the reactant names
         irtosr(j,nrxn)=ifindspc(name(irtosr(j,irxn)),suffix(i1:i2-1))
         if(irtosr(j,nrxn).le.0)then
          print*,'couldnt find expected reactant',tr,tp,aname(:1)
          print*,'basename,suffix:**'//name(irtosr(j,irxn))//'**'//
     +            suffix(i1:i2-1)//'**',i1,i2-1
          print*,'reactant list:',rxnlbl(irxn),nrtosr(irxn)
          do j2 = 1,nrtosr(irxn)
           if(irtosr(j2,irxn).gt.0)print*,name(irtosr(j2,irxn))
          enddo
          stop 'marker 1'
         endif
         i1 = i2
        else
         irtosr(j,nrxn) = irtosr(j,irxn)
        endif
       enddo
c
c write the reactants to the output buffer
c
       do j = 1,nrtosr(nrxn)
        if(irtosr(j,nrxn).gt.0)then
         sname = name(irtosr(j,nrxn))
        elseif(-irtosr(j,nrxn).gt.maxmax)then
         write(sname,'(a)')rxnlbl(-irtosr(j,nrxn)-maxmax)
         call movlft(sname)
         write(sname,'(a)')'#RCON'//sname(:nblank(sname))
        elseif(-irtosr(j,nrxn).gt.maxcov)then
         write(sname,'(f16.3)')coef(-irtosr(j,nrxn))
         call movlft(sname)
         write(sname,'(a)')'#'//sname(:nblank(sname))
        elseif(-irtosr(j,nrxn).ne.0)then
         write(sname,'(a)')coefnm(-irtosr(j,nrxn))
         call movlft(sname)
         write(sname,'(a)')'#'//sname(:nblank(sname))
        else
         write(icrt,*)'coefnm(0) is an error!'
         write(icrt,*)outbuf(:nblank(outbuf))
         write(icrt,*)(irtosr(i1,nrxn),i1=1,nrtosr(nrxn))
         stop 'marker1'
        endif
        call movlft(sname)
        jj = nblank(outbuf) + 2
        write(outbuf(jj:),'(a)')sname(:nblank(sname))
c add a plus sign for real species
        if(irtosr(j,nrxn).gt.0)then
         if(j.lt.nrtosr(nrxn))then
          jj = nblank(outbuf) + 2
          write(outbuf(jj:),'(a)')'+'
         endif
        endif
       enddo
c add an equal sign 
       jj = nblank(outbuf) + 2
       write(outbuf(jj:),'(a)')'='
c
c set the product numbers and coeficients.  this is tricky because we
c allow integer coefficients in the product list. jbase will be the 
c index count in the new rxn, while j is the index count from the 
c original rxn.
c
       i1 = 1
       jbase = nprods(nrxn)
       do j = nprods(irxn),nprods(irxn+1)-1
        icount = icount + 1
        if(tno(icount).gt.0)then
c count any leading coefficients and calculate their product
         c1 = 1.0
         do j2 = j-1,nprods(irxn),-1
          if(iprods(j2).gt.0)goto 40
          if(-iprods(j2).le.maxco)c1 = c1 * coef(-iprods(j2))
         enddo
 40      continue
c if we have integer leading coeficients then expand them.  check again
c to make sure fractional coeficients only occur when we have 1 typed
c reactant.  np is the number of products produced by current base product.
         if(amod(c1,1.0).eq.0.0)then
          nlc = j - 1 - j2 
          jbase = jbase - nlc
          np = int(c1)
         elseif(nr.eq.1)then
          np = 1
          i1 = 1
         else
          print*,'somehow a fractional product coeficient exists for ',
     +       'a reaction with more than 1 typed reactant',rxnlbl(irxn)
          stop 'marker2'
         endif
c loop over the products, expanding as necessary
         do 60 j2 = 1,np
c find the proper substring of the suffix; i1 is the start of the current
c substring, and i2 is the start of the next substring
          isum = 0
          do i2 = i1,len(suffix)
           if(suffix(i2:i2+1).eq.'_'//aname(:1) .or. 
     +        suffix(i2:i2).eq.' ')isum=isum+1
           if(isum.gt.tno(icount))goto 30
          enddo
 30       continue 
c now check the name
          iprods(jbase) = ifindspc(name(iprods(j)),suffix(i1:i2-1))
          if(iprods(jbase).le.0)stop 'could not find expected product'
          i1 = i2
          jbase = jbase + 1
 60      continue
        else
         iprods(jbase) = iprods(j)
         jbase = jbase + 1
        endif
       enddo
c
c write the products to the output buffer
c
       newrxn = .true.
       do j = nprods(nrxn),nprods(nrxn+1)-1
        if(iprods(j).gt.0)then
         sname = name(iprods(j))
        elseif(-iprods(j).gt.maxmax)then
         write(sname,'(a)')rxnlbl(-iprods(j)-maxmax)
         call movlft(sname)
         write(sname,'(a)')'#RCON'//sname(:nblank(sname))
        elseif(-iprods(j).gt.maxcov)then
         write(sname,'(f16.3)')coef(-iprods(j))
         call movlft(sname)
         write(sname,'(a)')'#'//sname(:nblank(sname))
        elseif(-iprods(j).ne.0)then
         write(sname,'(a)')coefnm(-iprods(j))
         call movlft(sname)
         write(sname,'(a)')'#'//sname(:nblank(sname))
        else
         write(icrt,*)'coefnm(0) is an error!'
         write(icrt,*)outbuf(:nblank(outbuf))
         write(icrt,*)(iprods(i1),i1=nprods(nrxn),nprods(nrxn+1)-1)
         stop 'marker2'
        endif
        call movlft(sname)
        jj = nblank(outbuf) + 2
        write(outbuf(jj:),'(a)')sname(:nblank(sname))
        jj = nblank(outbuf) + 2
        if(iprods(j).gt.0 .and. j.lt.nprods(nrxn+1)-1)then
         write(outbuf(jj:),'(a)')'+'
        endif
c intermediate dump of the output buffer if necessary
        jj = nblank(outbuf) + 2
        if(jj.ge.62 .and. j.lt.nprods(nrxn+1)-1)then
         write(outbuf(jj:),'(a)')'&'
         write(unit3)newrxn,jj,outbuf(:jj)
         if(newrxn)then
          write(out,'(1x,a6,1x,(a))')rxnlbl(nrxn),outbuf
          newrxn = .false.
         else
          write(out,'(10x,(a))')outbuf
         endif
         outbuf = ' '
        endif
       enddo
c
c final dump of the output buffer
c
       jj = nblank(outbuf)
       write(unit3)newrxn,jj,outbuf(:jj)
       write(out,'(1x,a6,1x,(a))')rxnlbl(nrxn),outbuf
c       print*,'outbuf:'
c       print*,'**'//outbuf(:nblank(outbuf))//'**'
c       stop 'debug point 1'
 10   continue ! bottom of the added reaction loop
c
c modify the base secondary reaction label to include the typed atom
c
      i1 = max(nblank(srxnlbl(irxn)),1)
      if(i1.gt.0)then
       write(srxnlbl(irxn),'(a)')srxnlbl(irxn)(:i1)//aname(:1)
      else
       write(srxnlbl(irxn),'(a)')aname(:1)
      endif
      call movlft2(srxnlbl(irxn))
c
c return to the calling subroutine
c
      return
       end

      subroutine chktspc2(atom,tno,i1,k)
c********************************************************************
c written by: Mike Kleeman (Nov 2004)
c             UC Davis CEE
c
c The purpose of this subroutine is to check and see if the species
c i1 has been typed for "atom" tracking base species k.  It will be 
c added to the species array of it is missing.  
c
c The suffix added to "basename" will be _X?_X?_X?... where X is
c the traced molecule (N, S, C, X) and ? is some number 1<?<Xno.  The 
c number of iterations is set by tno
c
c Note that the include file pspecs.inc has common blocks that pass 
c most of the i/o.
c
c Inputs:
c  atom  - atom being tracked (N, S, or C)
c  i1    - index number for species of interest
c  k     - base tracked species
c
c Outputs:
c
c********************************************************************

      include 'pspecs.inc'
      character atom*1,suffix*30,sufgen*30,name2*16

c--determine the number of atoms in the target species--
      n = int(tno)

c--check the number of atoms for limiting cases--
      if(n.eq.0)return

c--get the length of the basename and the number of typed species--
      l1 = nblank(name(i1))
      nt1 = ttypes(k) + 1
      nt = nt1**n

c--loop over all the index values and check to see if the species is
c  in the active list.  note: vanilla species 1 is skipped--
      do 10 j1 = 2,nt
       suffix = sufgen(j1,n,nt1,atom)
c hunt through the existing names for basename plus the target suffix
       i = ifindspc(name(i1),suffix)
       if(i.gt.0)goto 10
c if we reach this point, the typed species must be added
       do 20 i = 1,ns
c expand any species that matches the basename
        if(name(i)(:l1).eq.name(i1)(:l1) .and. 
     +     index(name(i),'_'//atom).eq.0 .and.
     +     (name(i)(l1+1:l1+1).eq.' ' .or. 
     +                name(i)(l1+1:l1+1).eq.'_') )then
c expand this species; note that spcnam will change ns, but this 
c doesn't affect the loop for i after it is initialized
         if(ns+1.gt.maxns) stop 'chktspc2 marker3: exceeded ns'
         write(name2,'(a)')name(i)(:nblank(name(i)))//
     +                     suffix(:nblank(suffix))
c         print*,'adding species **'//name2//'**'
         call spcnam(isp,name2,.true.)
         sptyp(isp) = sptyp(i)
         mwt(isp) = mwt(i)
         conc0(isp) = conc0(i)
         nno(isp) = nno(i)
         cno(isp) = cno(i)
         sno(isp) = sno(i)
         xno(isp) = xno(i)
         write(typ1, 1005) name(isp),sptyp(isp),mwt(isp),conc0(isp),
     +                     nno(isp),cno(isp),sno(isp)
 1005    format(2x,'Added species ',a16,i4,f8.3,f8.3,i4,f8.3,i4)
        endif
 20    continue
 10   continue

c--return to the calling subroutine--
      return
      end
C
C -----------------------------------------------------------------------------
C
       subroutine TRDATOM(ISP,ANAME)
c
c       Read in the atom to be typed and check to see if it's okay
c         make sure that the atom is either N,C,S, or -
c
       character*16 aname
C
        INCLUDE 'pspecs.inc'
C
       call movlft(aname)
       write(icrt,'a16') aname
       if(aname(2:2) .eq. ' ') then
         if(aname(1:1) .eq. 'n' .or. aname(1:1) .eq. 'N') then
           aname(1:1) = 'N'
         elseif(aname(1:1) .eq. 'c' .or. aname(1:1) .eq. 'C') then
           aname(1:1) = 'C'
         elseif(aname(1:1) .eq. 's' .or. aname(1:1) .eq. 'S') then
           aname(1:1) = 'S'
         elseif(aname(1:1) .eq. 'x' .or. aname(1:1) .eq. 'X') then
           aname(1:1) = 'X'
         else
c          none of the above and that's a problem what to do
           aname(1:1) = '*'
         endif
       else
c        is it written out as "Nitrogren" ?
         if(aname(1:8).eq.'nitrogen'.or.aname(1:8).eq.'Nitrogen') then
           aname(1:1) = 'N'
         elseif(aname(1:6).eq.'carbon'.or.aname(1:6).eq.'Carbon') then
           aname(1:1) = 'C'
         elseif(aname(1:6).eq.'sulfur'.or.aname(1:6).eq.'Sulfur') then
           aname(1:1) = 'S'
         elseif(aname(1:4).eq.'none'.or.aname(1:4).eq.'None'
     &                      .or.aname(1:4).eq.'NONE') then
           aname(1:1) = 'X'
         else
c          none of the above and that's a problem what to do
           aname(1:1) = '*'
         endif
       endif
c
       tatom(ISP)=aname(1:1)
c
c        finished so return
c
       return
       end
c
C -----------------------------------------------------------------------------
        SUBROUTINE TSPCNAM(ISP,SPNAME,RX)
C
C       DETERMINE SPECIES NO. FOR NAME IN TSPNAME.  RETURNS SPECIES NO. IN
C       ISP.  ADDS SPECIES TO NAME to type ARRAY IF NEW, AND DO OTHER UPDATING.
C
C       CALLED FROM:  RDRXN, TYPEO
C
C
C       ARGUMENTS
C
        LOGICAL   RX
        integer i
        CHARACTER*16 SPNAME
C
C       SPECIFICATIONS FOR PREPARATION PROGRAM VARIABLES, PARAMETERS, AND
C       ARRAYS
C
        INCLUDE 'pspecs.inc'
C
C
       IF (NST.EQ.0) GOTO 1125
       DO 1122 ISP=1,NST
       IF (TNAME(ISP).NE.SPNAME) GOTO 1122
D      WRITE (OUT,*) 'SPCNAM: (OLD) ',TNAME(ISP),'  SPC NO.=',ISP
       IF (.NOT.RX) THEN
C if there needs to be any processing of an already specified typeo species
C put it here.
       ENDIF
       RETURN
 1122  CONTINUE
C  -     NAME NOT FOUND - NEW SPECIES ADDED TO LIST
 1125  NST=NST+1
       IF (NST.GT.MAXNS) THEN
                I=MAXNS
                IF (RX) WRITE (OUT,1127) OUTBUF
 1127           FORMAT (' ',A80)
                WRITE (OUT,*) 'TOO MANY SPECIES.  MAX =',I
                STOP 'TOO MANY SPECIES'
       ENDIF
       TNAME(NST)=SPNAME
       IF (IND.GT.0) THEN
c        write in the default atom and number of sources
       ELSE
                WRITE (OUT,*) 'PGM ER. IND<0 AT TSPCNAM'
       ENDIF
       ISP=NST
       RETURN
       END


      function nblank(buf)
c*****************************************************************************
c
c written by: Mike Kleeman (May 2002)
c             Dept of Civil and Env Eng
c             UC Davis 95616
c
c The purpose of this function is to return the rightmost position of non
c blank characters in the input buffer.
c
c*******************************************************************************

      integer    nblank, ilen
      character* (*) buf
      nblank = -1
      ilen = len(buf)
      do i = ilen,1,-1
       if(buf(i:i).ne.' ')then
        nblank = i
        return
       endif
      enddo
      return
      end

      function sufgen(j1,n,nt1,atom)
c***************************************************************************
c  written by: Mike Kleeman (Nov 2004)
c              UC Davis CEE
c              Davis CA 95616
c
c The purpose of this function is to generate the suffix used in the 
c naming procedure for typed species.
c
c Inputs:
c  j1   - current count in all the possibilities
c  n    - number of target atoms 
c  nt1  - number of sources to be typed
c  atom - N, S, C, or - (name of the traced atom)
c
c Outputs:
c  sufgen - character* string with the required suffix
c***************************************************************************
      character* (*) sufgen
      character  atom*1
      integer i2(10)

c--check the size--
      if(n.gt.10)then 
       print*,'must increase the dimension of working vector '//
     +        'i2 in sufgen'
       stop 'marker2 in sufgen'
      endif

c--remember the original count--
      j2 = j1

c--basically convert the value of j1 to base nt1 and record in i2--
      do j3 = n,1,-1
       i2(j3) = (j2-1) / nt1**(j3-1)
       j2 = j2 - i2(j3)*nt1**(j3-1)
      enddo
c      print*,j1,(i2(j3),j3=n,1,-1)

c--write the results to the output string--
      sufgen = ' '
      do j3 = n,1,-1
       L = max(nblank(sufgen)+1,1)
       if(i2(j3).lt.10)then
        write(sufgen(L:),10)atom,i2(j3)
 10     format('_',a1,i1)
       elseif(i2(j3).lt.100)then
        write(sufgen(L:),20)atom,i2(j3)
 20     format('_',a1,i2)
       elseif(i2(j3).lt.1000)then
        write(sufgen(L:),30)atom,i2(j3)
 30     format('_',a1,i3)
       else
        print*,'more than 1000 sources doesnt seem reasonable'
        stop 'marker1 in sufgen'
       endif
      enddo

c--return to the calling subroutine--
      return
      end

      function ifindspc(basename,suffix)
c*************************************************************************
c written by: Mike Kleeman (Nov 2004)
c             UC Davis CEE
c             Davis CA 95616
c
c The purpose of this function is to find the index value of the species
c name that contains basename and suffix.  It is assumed that each suffix
c has the form _X?_X?_X? where X is the traced atom, and ? is the source 
c number.  Note that all vanilla species will have suffix _X0
c
c**************************************************************************
      include 'pspecs.inc'
      character* (*) basename, suffix
      integer    ifindspc

c--find the length of suffix--
      l2 = nblank(suffix)

c--check to see if the suffix is _X0 which signifies vanilla target for
c  this traced atom--
      if(l2.le.3 .and. suffix(l2:l2).eq.'0')then
c hunt through the species names for exact match on basename
c       print*,'hunting for name:**'//basename//'**'
       do i = 1,ns
        if(name(i).eq.basename)then 
         ifindspc = i
         return
        endif
       enddo
      else
c hunt through the species names for basename_suffix
       l1 = index(basename,'_')-1
       if(l1.le.0)l1 = nblank(basename)
c       print*,'hunting for name:**'//basename(:l1)//suffix(:l2)//'**'
       do i = 1,ns
        l3 = index(name(i),'_')-1
        if(l3.le.0)l3 = nblank(name(i))
        if(name(i)(:l3).eq.basename(:l1) .and.
     +     index(name(i),suffix(:l2)).gt.0)then
         ifindspc = i
         return
        endif
       enddo
      endif

c--species not found, so return empty--
      ifindspc = -1
      return
      end

      SUBROUTINE MOVLFT2 (BUF)
C
C     MOVES CHARACTERS ORIGINALY PADDED TO LEFT WITH
C     BLANKS SO THAT BLANKS ARE PADDED TO THE RIGHT INSTEAD.
C
      CHARACTER* (*) BUF
C
      ILEN = len(buf)
      DO 10 N=1,ILEN
      IF (BUF(N:N).NE.' ') GOTO 20
   10 CONTINUE
      RETURN
C
   20 IF (N.EQ.1) RETURN
      N=N-1
      L=ILEN-N
      DO 30 I=1,L
   30 BUF(I:I)=BUF(I+N:I+N)
C
      N=L+1
      DO 40 I=N,ILEN
   40 BUF(I:I)=' '
      RETURN
      END
C
C end of typeosubs.for
